apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = '1.6.21' // 版本

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }
}
dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')

    api rootProject.ext.dependencies["xdroid-mvp"]

    api rootProject.ext.dependencies["PersistentCookieJar"]
    api rootProject.ext.dependencies["qiujuer-ui"]
    api rootProject.ext.dependencies["multidex"]
    api rootProject.ext.dependencies["exifinterface"]
    api rootProject.ext.dependencies["constraintlayout"]

    api rootProject.ext.dependencies["navigation-fragment-ktx"]
    api rootProject.ext.dependencies["navigation-ui-ktx"]
    api rootProject.ext.dependencies["navigation-ui"]
    api rootProject.ext.dependencies["navigation-fragment"]
}

// 生成sourceJar和javaDocJar构件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = rootProject.groupId

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                name 'LennonUtill'
                url rootProject.website
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'lennon-utill'
                        name rootProject.groupId
                        email rootProject.email
                    }
                }
                scm {
                    connection rootProject.gitUrl
                    developerConnection rootProject.gitUrl
                    url rootProject.siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
boolean isHasFile = false
if (project.rootProject.file('local.properties') != null){
    isHasFile = true
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
bintray {
    user = isHasFile ? properties.getProperty("bintray.user") : System.getenv("bintray.user")
    key = isHasFile ? properties.getProperty("bintray.apikey") : System.getenv("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = rootProject.repoName
        name = "LennonUtill"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}